species: complex loving critter
memsize: 7
defense: 10
offense: 10
size: 1
energy: 1000000
posture: 10
1 = 1 --> mate;
POSTURE != 17 --> POSTURE := 17; // we are species 17!
nearby[3] = 0 and ENERGY > 2500 --> bud;
{ENERGY > SIZE * 400 and SIZE < 7} --> grow;
ahead[0] < -1 and ENERGY < 500 * SIZE --> eat;
// next line attacks only other species
(ahead[1] / 10 mod 100) != 17 and ahead[1] > 0 --> attack;
ahead[1] < -5 --> forward;
{ahead[2] < -10 or random[20] = 0} and ahead[1] = 0 --> forward;
ahead[3] < -15 and ahead[1] = 0 --> forward;
ahead[4] < -20 and ahead[1] = 0 --> forward;
nearby[0] > 0 and nearby[3] = 0 --> backward;
// karma action: donate food if we are too full or large enough
ahead[1] < -1 and { ENERGY > 2500 or SIZE > 7 } --> serve[ENERGY / 42];
random[3] = 1 --> left;
1 = 1 --> wait; // mostly soak up the rays